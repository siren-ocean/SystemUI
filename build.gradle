// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        kotlin_version = '1.4.0'
        dagger_version = "2.19"

        libs = [
                viewpager2                : 'androidx.viewpager2:viewpager2:1.0.0',
                recyclerview              : 'androidx.recyclerview:recyclerview:1.2.1',
                legacy_support_v4         : 'androidx.legacy:legacy-support-v4:1.0.0',
                legacy_preference_v14     : 'androidx.legacy:legacy-preference-v14:1.0.0',
                appcompat                 : 'androidx.appcompat:appcompat:1.4.0-alpha03',
                annotation                : 'androidx.annotation:annotation:1.1.0',
                mediarouter               : 'androidx.mediarouter:mediarouter:1.2.0-alpha01',
                lifecycle_runtime         : 'androidx.lifecycle:lifecycle-runtime:2.4.0-alpha03',
                lifecycle_extensions      : 'androidx.lifecycle:lifecycle-extensions:2.2.0',
                palette                   : 'androidx.palette:palette:1.0.0',
                constraintlayout          : 'androidx.constraintlayout:constraintlayout:2.0.1',
                constraintlayout_solver   : 'androidx.constraintlayout:constraintlayout-solver:2.0.1',
                leanback                  : 'androidx.leanback:leanback:1.1.0-alpha04',
                slice_builders            : 'androidx.slice:slice-builders:1.1.0-alpha02',
                slice_core                : 'androidx.slice:slice-core:1.1.0-alpha02',
                slice_view                : 'androidx.slice:slice-view:1.1.0-alpha02',
                core_runtime              : 'androidx.arch.core:core-runtime:2.1.0',
                dynamicanimation          : 'androidx.dynamicanimation:dynamicanimation:1.1.0-alpha03',

                core_ktx                  : 'androidx.core:core-ktx:1.3.1',
                kotlin_stdlib_jdk7        : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version",
                kotlinx_coroutines_android: "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_version",
                kotlinx_coroutines_core   : "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_version",
                kotlin_reflect            : "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version",

                dagger_android            : "com.google.dagger:dagger-android:$dagger_version",
                dagger_android_processor  : "com.google.dagger:dagger-android-processor:$dagger_version",
                dagger_compiler           : "com.google.dagger:dagger-compiler:$dagger_version"

        ]
    }

    repositories {
        maven {
            url 'https://maven.aliyun.com/repository/jcenter'
        }
        maven {
            url 'https://maven.aliyun.com/repository/central'
        }
        maven {
            url 'https://maven.aliyun.com/repository/google'
        }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:3.5.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion COMPILE_SDK
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.android.systemui"
        minSdkVersion 29
        targetSdkVersion 30
        versionCode 30
        versionName "11"
    }

    signingConfigs {
        platform {
            storeFile file("platform.keystore")
            storePassword '123456'
            keyAlias 'platform'
            keyPassword '123456'
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.platform
        }

        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.platform
        }

        applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "SystemUI.apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
                '-Xjvm-default=all-compatibility'
        ]
    }

    sourceSets {
        main {
            java.srcDirs = ['src']
            aidl.srcDirs = ['src']
            res.srcDirs = ['res-product', 'res-keyguard', 'res']
            manifest.srcFile 'AndroidManifest.xml'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compileOnly files('libs/framework.jar')
    compileOnly files('libs/core-all.jar')

    implementation files('libs/SystemUIPluginLib.jar')
    implementation files('libs/SystemUISharedLib.jar')
    implementation files('libs/WindowManager-Shell.jar')
    implementation files('libs/SystemUI-tags.jar')
    implementation files('libs/SystemUI-proto.jar')
    implementation files('libs/SystemUI-statsd.jar')
    implementation files('libs/libprotobuf-java-nano.jar')
    implementation files('libs/iconloader_base.jar')
    implementation project(':plugin_core')
    implementation project(':SettingsLib')
    implementation project(':SettingsLib:RestrictedLockUtils')
    implementation project(':SettingsLib:AdaptiveIcon')


    implementation(libs.legacy_support_v4, {
        exclude group: 'androidx.preference', module: 'preference'
    })
    implementation(libs.legacy_preference_v14, {
        exclude group: 'androidx.preference', module: 'preference'
    })

    implementation(name: 'preference-1.2.0-alpha01', ext: 'aar')
    implementation libs.viewpager2
    implementation libs.recyclerview
    implementation libs.appcompat
    implementation libs.mediarouter
    implementation libs.palette
    implementation libs.constraintlayout
    implementation libs.constraintlayout_solver
    implementation libs.leanback
    implementation libs.slice_builders
    implementation libs.slice_core
    implementation libs.slice_view
    implementation libs.core_runtime
    implementation libs.lifecycle_runtime
    implementation libs.lifecycle_extensions
    implementation libs.dynamicanimation

    implementation libs.kotlinx_coroutines_android
    implementation libs.kotlinx_coroutines_core
    implementation libs.kotlin_stdlib_jdk7
    implementation libs.kotlin_reflect
    implementation libs.core_ktx

    implementation libs.dagger_android
    kapt libs.dagger_android_processor
    kapt libs.dagger_compiler
}

allprojects {
    repositories {
        maven {
            url 'https://maven.aliyun.com/repository/jcenter'
        }
        maven {
            url 'https://maven.aliyun.com/repository/central'
        }
        maven {
            url 'https://maven.aliyun.com/repository/google'
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            if (options.bootstrapClasspath != null) {
                Set<File> fileSet = options.bootstrapClasspath.getFiles()
                List<File> newFileList = new ArrayList<>();
                newFileList.add(new File("./libs/framework.jar"))
                newFileList.add(new File("./libs/core-all.jar"))
                newFileList.addAll(fileSet)
                options.bootstrapClasspath = files(
                        newFileList.toArray()
                )
            }
        }
    }
}